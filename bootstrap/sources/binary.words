// ***************************************************************************************
// ***************************************************************************************
//
//		Name : 		binary.words
//		Author :	Paul Robson (paul@robsons.org.uk)
//		Date : 		3rd December 2018
//		Purpose :	Source code for bootstrap : Binary words
//
// ***************************************************************************************
// ***************************************************************************************

@word 	* 											// 16 bit multiply A = A * B
		push 	bc 									// save B and C
		push 	de
		ld 		b,h 								// HL -> BC
		ld 		c,l
		ld 		hl,$0000							// result
__MultiplyLoop:
		srl 	b 									// shift BC right
		rr 		c
		jr 		nc,__MultiplyNoAdd
		add 	hl,de 								// if bit 0 set add DE into result
__MultiplyNoAdd:
		ex 		de,hl 								// shift DE left
		add 	hl,de
		ex 		de,hl
		ld 		a,b 								// until BC is zero
		or 		c
		jr 		nz,__MultiplyLoop
		pop 	de 									// restore B and C
		pop 	bc
		ret

// =======================================================================================

@macro 	+ 											// 16 bit add A = A + B
		add 	hl,de

// =======================================================================================

@word 	< 											// A = -1 if B < A else 0
		ld 		a,d 								// look at bit 15's
		xor 	h
		jp 		m,__SignsDifferent 					// code if signs are different.
		push 	de
		ex 		de,hl
		xor 	a 									// do B-A calculation
		sbc 	hl,de
		pop 	de
		ld 		hl,$0000 							// return with 0 if NC
		ret 	nc
		dec 	hl
		ret

__SignsDifferent:
		bit 	7,d
		ld 		hl,$0000 							// if D is +ve it must be bigger
		ret 	z
		dec 	hl
		ret

// =======================================================================================

@word 	= 											// A = -1 if B = A else 0
		ld 		a,d 								// H = D ^ H
		xor 	h
		ld 		h,a
		ld 		a,e 								// A = (D^H) | (E^L)
		xor 	l
		or 		h
		ld 		hl,$0000	 						// A = 0 if different
		ret 	nz
		dec 	hl
		ret

// =======================================================================================

@word 	and
		ld 		a,h
		and 	d
		ld 		h,a
		ld 		a,l
		and 	e
		ld 		l,a
		ret

// =======================================================================================

@word 	or
		ld 		a,h
		or 		d
		ld 		h,a
		ld 		a,l
		or 		e
		ld 		l,a
		ret
				
// =======================================================================================

@word 	xor
		ld 		a,h
		xor 	d
		ld 		h,a
		ld 		a,l
		xor 	e
		ld 		l,a
		ret
				
