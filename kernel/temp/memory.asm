; ***************************************************************************************
; ***************************************************************************************
;
;  Name :   memory.src
;  Author : Paul Robson (paul@robsons.org.uk)
;  Date :   28th January 2019
;  Purpose : Memory operators
;
; ***************************************************************************************
; ***************************************************************************************



; ********* ! macro *********

define_21:
	call COMPMacroExpand
	ld b,end_21-start_21
start_21:
	ld   (hl),e
	inc  hl
	ld   (hl),d
	dec  hl
end_21:
	ret

; ***************************************************************************************



; ********* @ macro *********

define_40:
	call COMPMacroExpand
	ld b,end_40-start_40
start_40:
	ld   a,(hl)
	inc  hl
	ld  h,(hl)
	ld  l,a
end_40:
	ret

; ***************************************************************************************



; ********* +! word *********

define_2b_21:
	call COMPCompileSelf
	ld   a,(hl)
	add  a,e
	ld   (hl),a
	inc  hl
	ld   a,(hl)
	adc  a,d
	ld   (hl),a
	dec  hl
	ret

; ***************************************************************************************



; ********* c! macro *********

define_63_21:
	call COMPMacroExpand
	ld b,end_63_21-start_63_21
start_63_21:
	ld   (hl),e
end_63_21:
	ret

; ***************************************************************************************



; ********* c@ macro *********

define_63_40:
	call COMPMacroExpand
	ld b,end_63_40-start_63_40
start_63_40:
	ld   l,(hl)
	ld   h,0
end_63_40:
	ret

; ***************************************************************************************



; ********* p@ word *********

define_70_40:
	call COMPCompileSelf
	ld   b,d
	ld   c,e
	in   l,(c)
	ld   h,0
	ret

; ***************************************************************************************



; ********* p! macro *********

define_70_21:
	call COMPMacroExpand
	ld b,end_70_21-start_70_21
start_70_21:
	ld   b,d
	ld   c,e
	out  (c),l
end_70_21:
	ret
